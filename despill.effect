uniform float4x4 ViewProj;
uniform texture2d image;

uniform float spill<
    string label = "Spill Reduction";
    string widget_type = "slider";
    float minimum = 0;
    float maximum = 1.0;
    float step = 0.001;
> = 0.666;

uniform float chroma_hue<
    string label = "Hue Rotation";
    string widget_type = "slider";
    float minimum = -1;
    float maximum = 1;
    float step = 0.001;
> = 0;

uniform float luminance_correction<
    string label = "Luminance Correction";
    string widget_type = "slider";
    float minimum = 0;
    float maximum = 1;
    float step = 0.001;
> = 0.666;

uniform int type<
    string label = "Type";
    string widget_type = "select";
    int option_0_value = 0;
    string option_0_label = "Average";
    int option_1_value = 1;
    string option_1_label = "Double Red Average";  
    int option_2_value = 2;
    string option_2_label = "Double Blue Average";
    int option_3_value = 3;
    string option_3_label = "Blue Limit";
    int option_4_value = 4;
    string option_4_label = "Red Limit";
> = 0;

sampler_state textureSampler {
	Filter    = Linear;
	AddressU  = Clamp;
	AddressV  = Clamp;
};

struct VertData {
	float4 pos : POSITION;
	float2 uv  : TEXCOORD0;
};

VertData VSDefault(VertData v_in)
{
	VertData vert_out;
	vert_out.pos = mul(float4(v_in.pos.xyz, 1.0), ViewProj);
	vert_out.uv  = v_in.uv;
	return vert_out;
}

float3 ApplyHue(float3 col, float hueAdjust)
{
	float3 sqrt3_3 = float3(0.57735, 0.57735, 0.57735);
  float normalizedHue = hueAdjust * 6.28318530718;
	float cosAngle = cos(normalizedHue);
	// Rodrigues' rotation formula
	return col * cosAngle + cross(sqrt3_3, col) * sin(normalizedHue) + sqrt3_3 * dot(sqrt3_3, col) * (1.0 - cosAngle);
}

float4 PSDespill(VertData v_in) : TARGET
{
	float4 rgba = image.Sample(textureSampler, v_in.uv);

   // Shift so the chroma hue (that we want to remove) is always red.
  float3 normalizedRGB = ApplyHue(rgba.rgb, -chroma_hue);

  // Limit the red channel to the average of the green and blue channels.
  float v;
  if (type == 1) {
    v = (normalizedRGB.r * 2 + normalizedRGB.b) * 0.333333;
    if (normalizedRGB.g > v) normalizedRGB.g = v;
  } else if (type == 2) {
    v = (normalizedRGB.r + normalizedRGB.b * 2) * 0.333333;
    if (normalizedRGB.g > v) normalizedRGB.g = v;
  } else if (type == 3) {
    if (normalizedRGB.g > normalizedRGB.b) normalizedRGB.g = normalizedRGB.b;
  } else if (type == 4) {
    if (normalizedRGB.g > normalizedRGB.r) normalizedRGB.g = normalizedRGB.r;
  } else {
    v = (normalizedRGB.r + normalizedRGB.b) * 0.5;
    if(normalizedRGB.g > v) normalizedRGB.g = v;
  }
  
  // Now shift the hue back, and interpolate based on the spill value.
  float3 rgb = lerp(rgba.rgb, ApplyHue(normalizedRGB, chroma_hue), spill);

  float3 difference = abs(rgb - rgba.rgb);
  // Calculate luminance according to BT.709
  float luminance = (difference.r) * 0.2126 + (difference.g) * 0.7152 + (difference.b) * 0.0722;
  rgba.rgb = rgb + (luminance * luminance_correction);

  return rgba;
}

technique Draw
{
	pass
	{
		vertex_shader = VSDefault(v_in);
		pixel_shader  = PSDespill(v_in);
	}
}